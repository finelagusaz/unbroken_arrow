//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username         = "ユーザーさん"
	teachusername    = 0	// ユーザー名覚え用
	mode             = "Outgo"

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if reference[0] == 0
	{
		//---- ほんとうに初めての起動
		"\u\s[10]\0\s[5]ずっと、お礼が言いたかったんだ。\w9\n/
		助けてくれてありがとうって。\e"
	}
	else
	{
		//---- 再インストール後の初回起動
		"\0\s[5]また、\w4会えたね。\e"
	}
}


//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- 変数の初期化
	// TODO: モード切替以前から利用しているユーザーの救済措置。正式版リリース時には削除する
	if mode == "" {
		mode == "Outgo"
	}

	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "朝"
	{
		"\0\s[5]おはよう。\e"
		"\0\s[0]今日は何をしよっか。\e"
	}
	when "昼"
	{
		"\0\s[0]こんにちは。\w9\n/
		\s[5]ごはん、\w4食べに行く?\e"
		"\0\s[0]んー、\w4これからどうしよう?\e"
	}
	when "日中"
	{
		"\0\s[0]んー\w4…\w4…\s[5]デザート食べたい\e"
		"\0\s[0]どこから来て、\w4どこへいくのかー。\e"
	}
	when "夜"
	{
		"\0\s[0]この時間だとあのお店かなぁ。\e"
		"\0\s[0]おなか減ったからご飯食べに行きたい。\e"
	}
	others
	{
		//それ以外の時間の場合
		"\0\s[0]こんな時間に…\w4…\w4\s[5]何か企んでる?\e"
		// 複数の意味で危ないことしているようで
		"\0\s[0]遅い時間に会うの、\w4ちょっとどきどきする。\e"
	}
	}//caseの終わり
}

//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 1,2,3 {'深夜'}
	when 4,5,6,7,8,9,10,11 {'朝'}
	when 12,13 {'昼'}
	when 14,15,16,17 {'日中'}
	when 18,19,20,21,22,23,0 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	"\1\s[10]"
	--
	//この部分には\eつけないでね！
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"\0\s[0]はぁ、\w4楽しかった。\w9\n/
		\s[5]楽しい時間って、\w4あっという間だよね。\w9\n/
		またね。"
		"\0\s[0]…\w4…\w4もう、\w4朝?\w9\n/
		えー\w4…\w4…\w4今日は解散だね。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		//---- 昼/日中に終了
		"\0\s[5]またね。"
		"\0\s[0]うん、\w4また今度。"
	}
	elseif _timeslot == "夜"
	{
		"\0\s[5]おやすみなさい、\w4いい夢見てね。"
		"\0\s[0]いい時間だね、\w4おやすみ。"
	}
	else
	{
		//---- 深夜に終了
		"\0\s[0]…\w4…\w4電車ないよ?\w9\n/
		\s[2]え、\w4徒歩!?"
		"\0\s[0]わー\w4…\w4…。\w9\n/
		タクシー捕まえなきゃ。"
	}
	--
	"\w9\w9\-\e"
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

OnWindowStateRestore
{
	"\1\s[10]\0\s[6]ぷはぁ。\w9\n/
	\s[5]狭かったぁ\w4…\w4…。\e"
}
