//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenMenu
{
	"\0\s[0]ん‥‥?"
	"\0\s[5]なぁに?"
	"\0\s[0]何か気になるものでも見つけた?"
	--
	AYATEMPLATE.MenuItem("なんか話して","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("さっきの話は？","Menu_LASTTALK")
	}
	--
	AYATEMPLATE.MenuItem("しゃべり頻度を変えて","Menu_TALKINTERVAL")
	--
	AYATEMPLATE.MenuItem("場所を変えよう","Menu_MOVE")
	--
	AYATEMPLATE.MenuItem("他のゴーストと話す","Menu_GHOSTCOMSTART")
	--
	AYATEMPLATE.MenuItem("アネモネに話しかける","Menu_COMMUNICATEOPEN")
	--
	AYATEMPLATE.MenuItem("なんでもない","Menu_CANCEL",150)
}

//------------------------------------------------------------------------------
//メニュー選択タイムアウト
//------------------------------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[0]?\e"
}


//******************************************************************************
//各項目の処理内容
//******************************************************************************

//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
Menu_TALKINTERVAL
{
	"\0\s[0]どのくらいがいいのかな？"
	--
	AYATEMPLATE.MenuItem("うるさいくらい","Menu_TALKMAX",200)
	--
	AYATEMPLATE.MenuItem("ふつう","Menu_TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("静か","Menu_TALKPOOR")
	--
	AYATEMPLATE.MenuItem("黙ってて","Menu_TALKNONE")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

//---- アネモネに話しかける
Menu_COMMUNICATEOPEN
{
	"\0\s[0]なにかな?\![open,communicatebox]\e"
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- キャンセルされた
Menu_CANCEL
{
	"\0\s[0]うん。\e"
}

//---- トーク頻度
Menu_TALKMAX
{
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\0\s[5]さて、\w4どれから話そうかなー?\e"
}

Menu_TALKNORMAL
{
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\0\s[0]ふつうって難しいよね。\e"
}

Menu_TALKPOOR
{
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\0\s[0]ゆっくり話す、\w4ではなくて、\w4静かに\w4…\w4…?\e"
}

Menu_TALKNONE
{
	//---- 話さない
	aitalkinterval = 0
	"\0\s[0]君といるときの沈黙は嫌いじゃないよ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	//---- キャンセルされた
	"\0\s[0]うん。\e"
}

Menu_MOVE
{
	"\0\s[0]どこにいこうか?"
	--
	AYATEMPLATE.MenuItem("アネモネの家","Menu_HERPLACE",200)
	--
	AYATEMPLATE.MenuItem("街","Menu_Outgo")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

Menu_HERPLACE
{
	mode = "HerPlace"
	--
	"\0\s[0]ゆっくりするにはいいと思うよ。\e"
}

MenuOutgo
{
	mode = "Outgo"
	--
	"\0\s[0]どの辺までいこうか。\e"
}